# getting movie profile with directors, cast, reviews, and average rating
SELECT Movie {
    id,
    image,
    title,
    year,
    description,
    directors: {
        id,
        full_name,
        image,
    }
    ORDER BY Movie.directors@list_order EMPTY LAST
        THEN Movie.directors.last_name,
    cast: {
        id,
        full_name,
        image,
    }
    ORDER BY Movie.cast@list_order EMPTY LAST
        THEN Movie.cast.last_name,
    avg_rating,

    # computables
    reviews := (
        SELECT Movie.<movie {
            id,
            body,
            rating,
            author: {
                id,
                name,
                image,
            }
        }
        ORDER BY .creation_time DESC
    ),
}
FILTER .id = $0;


# getting a person profile with movies that this person acted in or directed
SELECT Person {
    id,
    full_name,
    image,
    bio,

    # computables
    acted_in := (
        WITH M := Person.<cast
        SELECT M {
            id,
            image,
            title,
            year,
            avg_rating
        }
        ORDER BY .year ASC THEN .title ASC
    ),
    directed := (
        WITH M := Person.<directors
        SELECT M {
            id,
            image,
            title,
            year,
            avg_rating
        }
        ORDER BY .year ASC THEN .title ASC
    ),
}
FILTER .id = $0;


# simple user profile with 3 latest comments
SELECT User {
    id,
    name,
    image,
    latest_reviews := (
        WITH UserReviews := User.<author
        SELECT UserReviews {
            id,
            body,
            rating,
            movie: {
                id,
                image,
                title,
                avg_rating
            }
        }
        ORDER BY .creation_time DESC
        LIMIT 3
    )
}
FILTER .id = $0;
